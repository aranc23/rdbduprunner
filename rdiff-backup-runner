#! /usr/bin/perl

# $Id: rdiff-backup-runner,v 1.28 2008/02/26 19:51:02 acox Exp $

use strict;
use warnings;
use Getopt::Long;
use logentry;
use File::Temp qw( mkdtemp mkstemp );
use Data::Dumper;
use Fcntl qw(:DEFAULT :flock); # import LOCK_* constants
use Env qw( HOME );
use List::Util qw( shuffle );
use Config::General;

our $TEST=1;
our $VERBOSITY=5;
our $TVERBOSITY=3;
our $LOCALHOST;
our $COMPARE=0;
our $RDIFF_BACKUP;
our $EXCLUDE_PATH='/etc/rdiff-backup';
our $PATH;
our $HOST;
our $DEST;
our $FORCE=0;
our $REMOVE;
our %INCREMENTS;
our ($VERBOSE,$DEBUG);
our $AVERAGE;
#our ($IFILES,$IFILESIZE,$DESTSIZECHANGE);
our $LISTOLDEST=0;
our $CHECK=0;
our $CLEANUP=0;
our $DEFMAXINC;
our $DEFMAXAGE;
our $LOCK_FILE="$HOME/rdiff-backup-runner.pid";

our $CONFIG_FILE="$HOME/rdbrc";
our %CONFIG;
our %BACKUPS;

GetOptions('test!'          => \$TEST,
           'verbosity=i'    => \$VERBOSITY,
           'v|terminal-verbosity=i' => \$TVERBOSITY,
           'localhost=s'    => \$LOCALHOST,
           'rdiff-backup=s' => \$RDIFF_BACKUP,
           'compare'        => \$COMPARE,
           'path=s'         => \$PATH,
           'host=s'         => \$HOST,
	   'dest=s'         => \$DEST,
#	   'root=s'         => \$ROOT,
           'force!'         => \$FORCE,
           'remove-oldest!' => \$REMOVE,
           'verbose!'       => \$VERBOSE,
           'debug!'         => \$DEBUG,
           'calculate-average' => \$AVERAGE,
	   'list-oldest'    => \$LISTOLDEST,
	   'check'          => \$CHECK, # perform a check-destination-dir on target
	   'config=s'       => \$CONFIG_FILE, # config file
	   'cleanup'        => \$CLEANUP,
	   'maxinc=i'       => \$DEFMAXINC,
	   'maxage=s'       => \$DEFMAXAGE,
	   'lockfile=s'     => \$LOCK_FILE,
          );

if(not defined $LOCALHOST) {
  $LOCALHOST=`hostname`;
  chomp $LOCALHOST;
  my @a=split(/\./,$LOCALHOST);
  @a > 1 and $LOCALHOST=$a[0];
}

logentry::OpenLog('local5','rdiff-backup-runner');
logentry('info','starting');

%CONFIG=new Config::General(-ConfigFile => $CONFIG_FILE,
			    -UseApacheInclude => 1,
			    -AutoTrue => 1,
			    -LowerCaseNames => 1)->getall() or die "unable to parse $CONFIG_FILE";

print Dumper \%CONFIG;

if(not defined $RDIFF_BACKUP) {
    if(defined $CONFIG{rdiffbackuppath}) {
	$RDIFF_BACKUP=$CONFIG{rdiffbackuppath};
    } else {
	$RDIFF_BACKUP='rdiff-backup'; # in our path we hope
    }
}
unless(-x $RDIFF_BACKUP) {
    logndie("unable to execute $RDIFF_BACKUP");
}

foreach my $bdtag (keys(%{$CONFIG{backupdestination}})) {
    my $bd=$CONFIG{backupdestination}{$bdtag};
}
   
foreach my $bs (values(%{$CONFIG{backupset}})) {
    unless(defined $$bs{host}) {
	logndie "you must specify a Host directive in each backupset, even if the backup is local";
    }
    my $host=(defined $$bs{host} ? $$bs{host} : $LOCALHOST);
    if(defined $HOST and $host !~ /$HOST/) {
	next;
    }
    my $sshcompress=(defined $$bs{compress} ? $$bs{compress} : 0);

    my $backupdest;
    if(defined $$bs{backupdestination}) {
	$backupdest=$$bs{backupdestination};
    } elsif(defined $CONFIG{defaultbackupdestination}) {
	$backupdest=$CONFIG{defaultbackupdestination};
    }
    unless(defined $backupdest and defined $CONFIG{backupdestination}{$backupdest} and -d $CONFIG{backupdestination}{$backupdest}{path}) {
	logndie("no backup root defined or does not exist: $backupdest".(defined $CONFIG{backupdestination}{$backupdest}{path} ? " ($CONFIG{backupdestination}{$backupdest}{path})" : ''));
    }
    if(defined $DEST and $backupdest !~ /$DEST/) {
	next;
    }
    my $backupdestpath=$CONFIG{backupdestination}{$backupdest}{path};
    
    foreach my $path (ref($$bs{path}) eq "ARRAY" ? @{$$bs{path}} : ($$bs{path})) {
      if(defined $PATH and $path !~ /$PATH/) {
	  next;
      }
      my $dest;
      my $tag='';
      if(defined $$bs{tag}) {
	  $dest=$$bs{tag};
	  $tag=$dest;
      } else {
	  $dest=$path;
	  $dest =~ s/\//\-/g;
	  $dest eq '-' and $dest='-root';
	  $tag=$host.$dest;
      }
      $dest=$backupdestpath.'/'.$tag;

      logentry('debug',"Host: $host Path: $path Tag: $tag Dest: $dest Root: $backupdestpath");

      $BACKUPS{$tag}={%{$bs}}; # very important to make a copy here
      $BACKUPS{$tag}{dest}=$dest;
      $BACKUPS{$tag}{path}=$path;
      $BACKUPS{$tag}{tag}=$tag;
      $BACKUPS{$tag}{host}=$host;
      $BACKUPS{$tag}{sshcompress}=$sshcompress;

      if($BACKUPS{$tag}{path} =~ /(home|home\/acox|home\/spin\/Maildir)$/) {
	  #push(@com,'--exclude-globbing-filelist',
	  #     $EXCLUDE_PATH.'/exclude-generic-home');
	  push(@{$BACKUPS{$tag}{excludes}},$EXCLUDE_PATH.'/exclude-generic-home');
      }
      push(@{$BACKUPS{$tag}{excludes}},
	   $EXCLUDE_PATH.'/exclude-'.$tag);
      
       if(defined $DEFMAXAGE) {
 	  # override!
 	  $BACKUPS{$tag}{maxage}=$DEFMAXAGE;
       } elsif(not defined $BACKUPS{$tag}{maxage} and
 	      defined $CONFIG{maxage}) {
 	  $BACKUPS{$tag}{maxage}=$CONFIG{maxage};
       }
       if(defined $DEFMAXINC) {
 	  # override!
 	  $BACKUPS{$tag}{maxinc}=$DEFMAXINC;
       } elsif(not defined $BACKUPS{$tag}{maxinc} and
 	      defined $CONFIG{maxinc}) {
 	  $BACKUPS{$tag}{maxinc}=$CONFIG{maxinc};
       }
  }
}
print Dumper \%BACKUPS;

my $avcom="$RDIFF_BACKUP --calculate-average";
foreach my $tag (sort backup_sort (keys(%BACKUPS))) {
    my $bh=$BACKUPS{$tag}; # for convenience of typing

  if(defined $AVERAGE) {
    $avcom.=" $BACKUPS{$tag}{dest}/rdiff-backup-data/session_statistics.*.data";
  } elsif(defined $REMOVE or $LISTOLDEST or $CLEANUP) {
      unless(-d $BACKUPS{$tag}{dest}) {
	  # skip non-existant destination directories
	  next;
      }
      my $c="rdiff-backup -l --parsable-output ".$BACKUPS{$tag}{dest};
      logentry('debug',$c);
      my @res=`$c`;
      #@res == 1 and next; # if there is only one increment, don't consider it for removal
      foreach my $ln (@res) {
	  chomp $ln;
	  $ln =~ /^(\d+)\sdirectory$/ or next;
	  if(exists $INCREMENTS{$1}) {
	      # this needs fixed... it doesn't really work!
	      $INCREMENTS{$1+.1}=$BACKUPS{$tag};
	      #print Dumper $INCREMENTS{$1};
	      #warn "conflict: $1 $tag $BACKUPS{$tag}{dest}";
	  }
	  $INCREMENTS{$1}=$BACKUPS{$tag};
	  push(@{$BACKUPS{$tag}{increments}},$1);
      }
  } else { # actually do a  backup
      my @com;
      if($$bh{disabled}) {
	  # skip disabled backups
	  next;
      }
      @com=($RDIFF_BACKUP,
	    '--verbosity',$VERBOSITY,
	    '--terminal-verbosity',$TVERBOSITY,
	    '--exclude-special-files',
	    '--exclude-other-filesystems',
	  );
      $BACKUPS{$tag}{sshcompress} or push(@com,'--ssh-no-compression');
      $VERBOSE and push(@com,'--print-statistics');

      if($COMPARE) {
	  push(@com,'--compare');
      }

      foreach my $f (@{$BACKUPS{$tag}{excludes}}) {
	  push(@com,'--exclude-globbing-filelist',
	       $f);
      }
      # this is fugly:
      push(@com,($BACKUPS{$tag}{host} eq $LOCALHOST ? $BACKUPS{$tag}{path} : $BACKUPS{$tag}{host}.'::'.$BACKUPS{$tag}{path}),$BACKUPS{$tag}{dest});

      logentry('info',join(" ",@com));

      unless($TEST) {
	  lock_pid_file();

	  if($CHECK) {
	      @com=($RDIFF_BACKUP,
		    '--verbosity',$VERBOSITY,
		    '--terminal-verbosity',$TVERBOSITY,
		    '--check-destination-dir',$BACKUPS{$tag}{dest});
	      logentry('info',join(" ",@com));
	  } else {
	      if ($BACKUPS{$tag}{host} ne $LOCALHOST) {
		  system("/usr/sbin/fping -q $BACKUPS{$tag}{host}");
		  unless($? == 0) {
		      logentry('warning',"fping failed to ping $BACKUPS{$tag}{host}");
		      next;
		  }
	      }
	      if (defined $$bh{prerun}) {
		  print $$bh{prerun}."\n";
		  system($$bh{prerun});
		  unless($? == 0) {
		      logentry('err',"unable to execute prerun command: skipping backup!");
		      next;
		  }
	      }
	  }
	  system(@com);
	  my $mainret=$?;
	  unless($mainret == 0) {
	      logentry('err',"unable to execute rdiff-backup!");
	  }
	  unless($CHECK) {
	      if(defined $$bh{postrun}) {
		  print $$bh{postrun}."\n";
		  system($$bh{postrun});
		  unless($? == 0) {
		      logentry('err',"unable to execute postrun command");
		  }
	      }
	  }
	  unlink $LOCK_FILE;
	  flock(LOCK,LOCK_UN);
	  close LOCK;
      }
  }
}

if(defined $AVERAGE) {
    exec($avcom);
}

if(defined $REMOVE) {
  foreach my $t (sort { $a <=> $b } (keys(%INCREMENTS))) {
    if(scalar @{$INCREMENTS{$t}{increments}} == 1) {
	# cannot remove the only increment!
	next;
    }
    logentry('debug',"$t $INCREMENTS{$t}{dest} $INCREMENTS{$t}{tag}");
    my @com=($RDIFF_BACKUP,
	     '--verbosity',$VERBOSITY,
	     '--terminal-verbosity',$TVERBOSITY,
	     '--remove-older-than',($t+1),
	     $INCREMENTS{$t}{dest});
    logentry('info',join(" ",@com));
    unless($TEST) {
      system(@com);
      unless($? == 0) {
	logentry('err',"unable to execute rdiff-backup!");
      }
    }
    last;
  }
}

if($LISTOLDEST) {
  foreach my $t (sort { $a <=> $b } (keys(%INCREMENTS))) {
    logentry('info',localtime($t).' '.$INCREMENTS{$t}{dest}.' '.$INCREMENTS{$t}{tag});
  }
}


if($CLEANUP) {
    my $now=time();
    foreach my $tag (shuffle(keys(%BACKUPS))) {
	if(defined $BACKUPS{$tag}{maxinc} and
	   $BACKUPS{$tag}{maxinc} =~ /^\d+$/ and
	   @{$BACKUPS{$tag}{increments}} > $BACKUPS{$tag}{maxinc} ) {
	    # too many!
	    logentry('info',"$tag\t Incs: ".
		     scalar @{$BACKUPS{$tag}{increments}}.
		     "\t Max: ".$BACKUPS{$tag}{maxinc});
	    my $lastinc=(sort {$a <=> $b} (@{$BACKUPS{$tag}{increments}}))[$BACKUPS{$tag}{maxinc}-1];
	    logentry('debug',"last time to keep for $tag: ".localtime($lastinc));
	    my @com=($RDIFF_BACKUP,
		     '--verbosity',$VERBOSITY,
		     '--terminal-verbosity',$TVERBOSITY,
		     '--remove-older-than',$lastinc,
		);
	    if($FORCE) {
		push(@com,'--force');
	    }
	    push(@com,$BACKUPS{$tag}{dest});
	    logentry('info',join(" ",@com));
	    unless($TEST) {
		system(@com);
		unless($? == 0) {
		    logentry('err',"unable to execute rdiff-backup!");
		}
	    }
	}
	if(defined $BACKUPS{$tag}{maxage} and
	   $BACKUPS{$tag}{maxage} =~ /^\d/) {
	    my @com=($RDIFF_BACKUP,
		     '--verbosity',$VERBOSITY,
		     '--terminal-verbosity',$TVERBOSITY,
		     '--remove-older-than',$BACKUPS{$tag}{maxage},
		);
	    if($FORCE) {
		push(@com,'--force');
	    }
	    push(@com,$BACKUPS{$tag}{dest});


	    logentry('info',join(" ",@com));
	    unless($TEST) {
		system(@com);
		unless($? == 0) {
		    logentry('err',"unable to execute rdiff-backup!");
		}
	    }
	    
	}
    }
}


sub backup_sort {
    my $aprio=(defined $BACKUPS{$a}{priority} ? $BACKUPS{$a}{priority} : 0);
    my $bprio=(defined $BACKUPS{$b}{priority} ? $BACKUPS{$b}{priority} : 0);
    my $ahost=(defined $BACKUPS{$a}{host} ? $BACKUPS{$a}{host} : $LOCALHOST);
    my $bhost=(defined $BACKUPS{$b}{host} ? $BACKUPS{$b}{host} : $LOCALHOST);

    if($aprio == $bprio and $ahost eq $bhost) {
	return $BACKUPS{$a}{path} cmp $BACKUPS{$b}{path};
    } elsif($aprio == $bprio) {
	return $ahost cmp $bhost;
    } # else, compare the prio
    return $aprio <=> $bprio;
}

#sub find_backup_by_tag {
#    foreach my $hr (@BACKUPS) {
#	if

sub lock_pid_file {
    if(defined $LOCK_FILE) {
	# leave it alone
    } elsif(defined $CONFIG{LockFile}) {
	$LOCK_FILE=$CONFIG{LockFile};
    } else {
	$LOCK_FILE="$HOME/rdiff-backup-runner.pid";
    }
    unless(open(LOCK,'+<'.$LOCK_FILE) or open(LOCK,'>'.$LOCK_FILE)) {
	logentry('err',"unable to open pid file: $LOCK_FILE for writing");
	next;
    }
    unless(flock(LOCK,LOCK_EX|LOCK_NB)) {
	logentry('err',"unable to flock the pid file: $LOCK_FILE");
	next;
    }
    truncate(LOCK,0); # this shouldn't fail if we have the file opened and locked!
    print LOCK $$."\n"; # who really cares if this fails?
}
