#! /usr/bin/perl
# Copyright 2020 Aran Cox <arancox@gmail.com>

# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.

use strict;
use warnings;
use v5.16;

use Backup::rdbduprunner ':all';

# this should cause dd to die when sent kill, ctrl-c etc.
use sigtrap qw(die normal-signals);

#from a standard perl distribution, on UNIX at least
use English qw( -no_match_vars );
use Getopt::Long qw(:config pass_through) ; # use this to pull out the config file
use Data::Dumper;
use Env qw( HOME DEBUG );
use Pod::Usage;
eval { use Time::HiRes qw( time ); };
use File::Basename;


# we can create our own directories if needed:
for my $dir ($STATE_DIR,$CONFIG_DIR,$LOCK_DIR,$LOG_DIR) {
    unless ( -d $dir ) {
        my @created;
        unless ( @created = make_path($dir, { mode => 0700 }) ) {
            die "unable to create directory: ${dir}";
        }
        unless (string_any($dir,@created)) {
            warn @created;
            die "make_path did not return the expected result trying to create ${dir}";
        }
    }
}

# push the options onto the big get_options hash for passing to GetOptions
foreach my $key (keys(%cfg_def)) {
  my $o = $cfg_def{$key};
  $get_options{$$o{'cli'}} = $$o{'var'};
}

print STDERR Dumper \%get_options if $DEBUG;
GetOptions(%get_options);

# print the SYNOPSIS section and exit
pod2usage(-1) if $HELP;

if(not defined $LOCALHOST) {
  if(defined $CONFIG{localhost}) {
    $LOCALHOST=$CONFIG{localhost};
  } else {
    $LOCALHOST=`hostname`;
    chomp $LOCALHOST;
    my @a=split(/\./,$LOCALHOST);
    @a > 1 and $LOCALHOST=$a[0];
  }
}


create_dispatcher( $APP_NAME, $FACILITY, $LOG_LEVEL, $LOG_FILE );
$RUNTIME=time();
dlog('info','starting',{});

unless ( $CONFIG_FILE ) {
 FILE:
    foreach my $c (@CONFIG_FILES) {
        # loop through the candidates, choose the first existing one
        if ( -f $c ) {
            $CONFIG_FILE = $c;
            last FILE;
        }
    }
}
unless( $CONFIG_FILE and -f $CONFIG_FILE ) {
    my $msg= "no config files found in any locations, unable to continue!";
    critical($msg);
    die $msg;
}
%CONFIG=new Config::General(-ConfigFile => $CONFIG_FILE,
			    -IncludeGlob => 1,
			    -AutoTrue => 1,
			    -LowerCaseNames => 1)->getall() or die "unable to parse $CONFIG_FILE";
if($DUMP) {
  print Dumper \%CONFIG;
}

#my %co = %{dclone(\%config_definition)};

#foreach my $k (keys(%{$co{default}{fields}})) {
#delete $co{default}{fields}{$k} if ($co{default}{fields}{$k}{type} =~ m{^table}xms);
#}
#print STDERR Dumper \%co if $DEBUG;

#my $co = Config::Validator->new(%co);
#print STDERR Dumper [$co->options("default")] if $DEBUG;

my $config_validator = Config::Validator->new(%config_definition);

#$config_validator->traverse(sub {say "traverse:";print STDERR Dumper \@_[1..]}, \%CONFIG, 'default');

$config_validator->validate(\%CONFIG, 'default');

# set some global options using the config file global options???
if(not defined $DUPLICITY_BINARY) {
    if(defined $CONFIG{duplicitybinary}) {
	$DUPLICITY_BINARY=$CONFIG{duplicitybinary};
    } else {
	$DUPLICITY_BINARY='duplicity'; # in our path we hope
    }
}

if(not defined $RDIFF_BACKUP_BINARY) {
    if(defined $CONFIG{rdiffbackupbinary}) {
	$RDIFF_BACKUP_BINARY=$CONFIG{rdiffbackupbinary};
    } else {
	$RDIFF_BACKUP_BINARY='rdiff-backup'; # in our path we hope
    }
}

if(not defined $RSYNC_BINARY) {
  if(defined $CONFIG{rsyncbinary}) {
	$RSYNC_BINARY=$CONFIG{rsyncbinary};
  } else {
	$RSYNC_BINARY='rsync'; # in our path we hope
  }
}

if(not defined $ZFS_BINARY) {
  if(defined $CONFIG{zfsbinary}) {
	$ZFS_BINARY=$CONFIG{zfsbinary};
  } else {
	$ZFS_BINARY='zfs'; # in our path we hope
  }
}

unless(defined $VERBOSITY) { # from the command line
  if(defined $CONFIG{verbosity}) {
    $VERBOSITY=$CONFIG{verbosity};
  }
}

unless(defined $TVERBOSITY) { # from the command line
  if(defined $CONFIG{terminalverbosity}) {
    $TVERBOSITY=$CONFIG{terminalverbosity};
  }
}

if(defined $CONFIG{allowfs}) {
  @ALLOW_FS = ref($CONFIG{allowfs}) eq "ARRAY" ? @{$CONFIG{allowfs}} : ($CONFIG{allowfs} );
}

if(defined $EXCLUDE_PATH) {
  # leave it alone, it comes from the command line
} elsif(defined $CONFIG{excludepath}) {
  $EXCLUDE_PATH=$CONFIG{excludepath};
} else {
  $EXCLUDE_PATH='/etc/rdbduprunner';
}

if(not defined $USEAGENT) {
  if(defined $CONFIG{useagent}) {
    $USEAGENT=1;
  } else {
    $USEAGENT=0;
  }
}

if(not defined $TEMPDIR) {
  if(defined $CONFIG{tempdir}) {
    $TEMPDIR=$CONFIG{tempdir};
  }
}

if(not defined $MAXPROCS) {
    if(defined $CONFIG{maxprocs}) {
	$MAXPROCS=$CONFIG{maxprocs};
    } else {
	$MAXPROCS=1;
    }
}

if(not defined $MAXWAIT) {
    if(defined $CONFIG{maxwait}) {
	$MAXWAIT=$CONFIG{maxwait};
    } else {
	$MAXWAIT=86400; # one day's worth of seconds
    }
}

# combine the list of filesystems to skip into a regex
$SKIP_FS_REGEX='^('.join('|',map(quotemeta,@SKIP_FS)).')$';

dlog('debug','config',\%CONFIG,{'config_file' => $CONFIG_FILE});

if ( @STATUS_DELETE and scalar @STATUS_DELETE > 0 ) {
    status_delete(@STATUS_DELETE);
    exit;
}
elsif ( basename($PROGRAM_NAME) eq 'check_rdbduprunner' or $STATUS_JSON ) {
    status_json();
    exit;
}

@BACKUPS = parse_config_backups();
if($DUMP) {
  print Dumper \@BACKUPS;
  notice("you asked me to dump and exit!");
  exit(0);
}

if($STATUS) {
  foreach my $bh (sort backup_sort (@BACKUPS)) {
    my @com;
    if($$bh{btype} eq 'duplicity') {
      @com=($DUPLICITY_BINARY,'collection-status');
      $USEAGENT and push(@com,'--use-agent');
    } elsif($$bh{btype} eq 'rdiff-backup') {
      @com=($RDIFF_BACKUP_BINARY,'--list-increment-sizes');
    } elsif($$bh{btype} eq 'rsync') {
      @com=('du','-cshx');
    }
    unless($$bh{btype} eq 'rsync') {
      push(@com,verbargs($bh));

    }
    push(@com,$$bh{dest});
    info(join(" ",@com));
    unless($TEST) {
      my $lock=lock_pid_file($$bh{host});
      set_env($bh);
      system(@com);
      unless($? == 0) {
        error('unable to execute '.$com[0].'!');
      }
      unlock_pid_file($lock);
    }
  }
} elsif($LISTOLDEST) {
    build_increment_list();
    foreach my $ih (sort { $$a{inctime} <=> $$b{inctime} } (@INCREMENTS)) {
        print localtime($$ih{inctime}).' '.$$ih{bh}{dest}.' '.$$ih{tag}."\n";
    }
} elsif($REMOVE) {
    build_increment_list();
    remove_oldest('any');
}
elsif($ORPHANS) {
  foreach my $bh (sort backup_sort (@BACKUPS)) {
    if($$bh{btype} eq 'rdiff-backup') {
      my @com=('find',$$bh{dest},'-type','f','-name','rdiff-backup.tmp.*');
      info(join(" ",@com));
      system(@com);
    }
  }
} elsif($CLEANUP) {
    foreach my $bh (sort backup_sort (@BACKUPS)) {
        my @com;
        if($$bh{btype} eq 'duplicity') {
            push(@com,$DUPLICITY_BINARY,'cleanup');
            $USEAGENT and push(@com,'--use-agent');
            if($FORCE) {
                push(@com,'--force');
            }
        } elsif($$bh{btype} eq 'rdiff-backup') {
            push(@com,$RDIFF_BACKUP_BINARY,'--check-destination-dir');
        }
        else {
          warn("cleanup function only implmented for duplicity and rdiff-backup");
          next;
        }
        if(defined $TEMPDIR) {
          if(-d $TEMPDIR) {
            if($$bh{btype} eq 'rsync') {
              push(@com,"--temp-dir=$TEMPDIR");
            } else {
              push(@com,'--tempdir',$TEMPDIR);
            }
          } else {
            warn("specified temporary directory does not exist, not using it");
          }
        }
        push(@com,verbargs($bh),
             $$bh{dest});
        info(join(" ",@com));
        unless($TEST) {
            my $lock=lock_pid_file($$bh{host});
            set_env($bh);
            system(@com);
            unless($? == 0) {
                error("unable to execute $$bh{btype}!");
            }
            unlock_pid_file($lock);
        }
    }
} elsif ($TIDY) {
    foreach my $bh (sort backup_sort (@BACKUPS)) {
      my $lock;
      unless($TEST) {
	$lock=lock_pid_file($$bh{host});
      }
        tidy($bh);
      unless($TEST) {
	unlock_pid_file($lock);
      }
    }
} elsif($AVERAGE) {
    my $avcom="$RDIFF_BACKUP_BINARY --calculate-average";
    foreach my $bh (sort backup_sort (@BACKUPS)) {
        unless($$bh{btype} eq 'rdiff-backup') {
          warn("average function only applies to rdiff-backup type backupes");
            next;
        }
        $avcom.=" $$bh{dest}/rdiff-backup-data/session_statistics.*.data";
    }
    exec($avcom);
} elsif($COMPARE) {
  foreach my $bh (sort backup_sort (@BACKUPS)) {
    my @com;
    if ($$bh{btype} eq 'duplicity') {
      @com=($DUPLICITY_BINARY,'verify');
      $USEAGENT and push(@com,'--use-agent');
    }
    elsif($$bh{btype} eq 'rdiff-backup') {
      @com=($RDIFF_BACKUP_BINARY,'--compare','--no-eas');
    }
    else {
      warn("verify function not implemented for rsync backups");
      next;
    }
    push(@com,verbargs($bh),'--exclude-other-filesystems');
    foreach my $f (@{$$bh{excludes}}) {
      push(@com,'--exclude-globbing-filelist',
	   $f);
    }
    if ($$bh{btype} eq 'duplicity') {
      push(@com,$$bh{dest},$$bh{path});
    } else {
      unless($$bh{sshcompress}) {
	push(@com,'--ssh-no-compression');
      }
      push(@com,'--exclude-device-files',$$bh{src},$$bh{dest});
    }
	
    info(join(" ",@com));
    unless($TEST) {
      my $lock=lock_pid_file($$bh{host});
      set_env($bh);
      system(@com);
      my $mainret=$?;
      unless($mainret == 0) {
	error("$$bh{btype} exited with an error");
      }
      unlock_pid_file($lock);
    }
  }
} elsif($LIST) {
  foreach my $bh (sort backup_sort (@BACKUPS)) {
    my @com;
    unless($$bh{btype} eq 'duplicity') {
      warn("list function only applies to duplicity type backups");
      next;
    }
    @com=($DUPLICITY_BINARY,'list-current-files');
    $USEAGENT and push(@com,'--use-agent');
    push(@com,verbargs($bh));
    push(@com,$$bh{dest});
    info(join(" ",@com));
    unless($TEST) {
      my $lock=lock_pid_file($$bh{host});
      set_env($bh);
      system(@com);
      unless($? == 0) {
        error('unable to execute '.$com[0].'!');
      }
      unlock_pid_file($lock);
    }
  }
} else {
  # here we will eventually just perform the backups
  # first we check for space on rdiff-backup destinations and free some up,
  # before forking away in perform_backups
  foreach my $bh (@BACKUPS) {
    if($$bh{btype} eq 'rdiff-backup') {
      if($$bh{dest} =~ /\:\:/) {
        info("we are assuming the destination $$bh{dest} is remote and will not attempt to manage it's disk space");
      } else {
        while(1) {
          my $ans=check_space($$bh{backupdestination});
          if($ans == -1) {
            error("unable to determine if this backupdestination ($$bh{backupdestination}) has enough free space");
            error("no backups to this backupdestination will be attempted and this message will be repeated only once");
            $CONFIG{$$bh{backupdestination}}{busted}=1;
            next BACKUP;
          } elsif($ans == 0) {
            unless(remove_oldest($$bh{backupdestination})) {
              # we failed to remove an increment from the backupdestination
              # we cannot do backups on this bd for this run!
              warning("unable to remove an increment on backupdestination ($$bh{backupdestination}:$CONFIG{backupdestination}{$$bh{backupdestination}}{path})");
              warning("no further attempts will be made to do backups to this destination");
              $CONFIG{$$bh{backupdestination}}{busted}=1;
              next BACKUP;
            }
            # we can just fall out now and let check_space() run again to see if it helped
          } elsif($ans == 1) {
            # we have enough space to proceed
            last;
          }
        }
      }
    }
  }
  perform_backups(@BACKUPS);
}

dlog('info',
     'exiting',
     {'total_run_time_seconds' => time()-$RUNTIME});


=head1 NAME

rdbduprunner - perl magic wrapper for rsync, rdiff-backup, and duplicity

=head1 SYNOPSIS

B<rdbduprunner> {B<-h|--help>}

B<rdbduprunner> {B<--calculate-average>|B<--check>|B<--cleanup>|B<--compare>|B<--verify>|B<--dry-run>|B<--checksum>|B<--dump>|B<--list>|B<--list-oldest>|B<--orphans>|B<--remove-oldest>|B<--status>|B<--tidy>} {B<--test>|B<--notest>} {B<--config> I<path>} {B<--dest> I<regex>} {B<--host> I<regex>} {B<--path> I<regex>} {B<--facility> I<user|daemon|...>} {B<--level> I<info|warn|...>} {B<--lockfile> I<path>} {B<-t|--terminal-verbosity> I<integer>} {B<--verbosity> I<integer>} {B<-v|--verbose>} {B<--progress>} {B<--maxinc> I<integer>} {B<--maxage> I<timespec>}  {B<--tempdir> I<path>} {B<--full>} {B<--force>} {B<--maxprocs> I<integer>} {B<--maxwait> I<integer>} =item {B<--allow-source-mismatch>} {B<-n|--dry-run>} {B<-c|--checksum|--no-checksum>} {B<--inplace>|B<--no-inplace>} {B<--whole-file>|B<--no-whole-file>} {B<--stats>|B<--nostats>} {B<--skipfs> I<filesystem>} {B<--allowfs> I<filesystem>}

=head1 DESCRIPTION

rdbduprunner is a wrapper for rsync, rdiff-backup, and duplicity
backups.  By default rdbduprunner will get all it's configuration from
~/.rdbduprunner.rc using an apache-esque config file format parsed by
the Config::General module.

General usage involves running rdbduprunner with no options which will
only print out warnings and the commands that would be invoked if you
were to run it with the --notest option. Then run it again with the
--notest option to see the output.

Other common options involve filtering which backups to run, adjusting
the verbosity of the output, adjusting logging, and so on.


=head1 OPTIONS

=head2 MAJOR MODES

rdbduprunner's primary mode is "backup" and the following override
that; only one of the following is allowed for any invocation of
rdbduprunner.  Many only apply to one or both of rdiff-backup or
duplicity since they act on archives of their own creation.  For rsync
backups tools like ls and du perform similar functions.

=over 4

=item {B<--status-json>}

Prints the status DBM as a json hash for parsing by other tools.

=item {B<--status-delete>=I<backup_src>}

Deletes the specified keys from the status DBM thus making
rdbduprunner "forget" about them.  Does not change in any way what
rdbduprunner will back up in the future, only alters the status
database used by monitoring tools (cmk).

IE: rdbduprunner --status-delete /local/

Can be specified multiple times.


=item {B<--calculate-average>} (rdiff-backup only)

Parses run files from rdiff-backup to calculate average increment
size.  If you do not limit which backups this option applies to using
--dest, --host, or --path options you will get the average increment
size of all backups that rdbduprunner is configured to run with
rdiff-backup.


=item {B<--check>|B<--cleanup>} (rdiff-backup/duplicity only)

Both options are identical.  duplicty backups will have the cleanup
option run on the destination directory.  For rdiff-backup the
--check-destination-dir option will be run against the directory.

=item {B<--compare>|B<--verify>} (rdiff-backup/duplicity only)

Passes the compare option to duplicty and the --verify option to
rdiff-backup which both have the purpose of printing what has changed
since the last backup, in other words what would be backed up on the
next run.

=item {B<--dump>}

Simply dumps the internal data structures for backups and
configuration options and exits.  Meant purely for debugging.

=item {B<--list>} (duplicity only)

Passes the list-current-files option to duplicity, since duplicity
stores everything inside a tar file.

=item {B<--list-oldest>} (rdiff-backup only)

Lists all increments from all rdiff-backup type backups in the order
of their age.

=item {B<--orphans>} (rdiff-backup only)

Find orphaned rdiff-backup temporary files inside backup destinations
and print those file names to stdout (for deletion, likely.)

=item {B<--remove-oldest>} (rdiff-backup only)

Removes the oldest known backup from all known rdiff-backup type
backups.  Should remove only one backup regardless of other settings.
Meant primarily for testing?  Ignores all other settings for cleaning
up rdiff-backup style backups. This option honors the --test/--notest
flags and will only print how rdiff-backup would be run if --notest
isn't specified as well.

=item {B<--status>}

Works on all backup types.  On rsync invokes du, on duplicity uses the
collection-status option and on rdiff-backup uses the
--list-increment-sizes option.  This option requires --notest to
execute, primarily because of the time it takes to run the operations.

=item {B<--tidy>} (rdiff-backup/duplicity only)

Attempts to apply the settings for a backup regarding the maximum
number of increments or maximum age of an increment to all or
specified backups.

=back

=head2 SUB OPTIONS

=over 4

=item {B<--test>|B<--notest>}

The default invocation assumes --test which causes rdbduprunner to
print which commands it would run.  Passing --notest will cause
rdbduprunner to actually run the backup commands.

=item {B<--dest> I<regex>}

This option causes the backups to be run to be filtered based on the
BackupDestination.

=item {B<--host> I<regex>}

This option causes the backups to be run to be filtered based on the
Host.

=item {B<--path> I<regex>}

This option causes the backups to be run to be filtered based on the
path on the host to be backed up.

=item {B<-t|--terminal-verbosity> I<integer>}

Increases logging verbosity to the terminal with rdiff-backup or
duplicity backups.

=item {B<--verbosity> I<integer>}

Increases logging verbosity to the rdiff-backup/duplicity log files.

=item {B<-v|--verbose>} (rsync only)

Passes -v to rsync invocations.

=item {B<--progress>} (rsync only)

Passes --progress to rsync invocations.

=item {B<--facility> I<user|daemon|...>}

Default: user
Changes the facility as passed to the syslog subsystem.

=item {B<--level> I<debug|info|notice|warning|error|critical|alert|emergency>}

Default: info
Syslog severity level which is passed to the logging dispatcher. Log
messages of the specified severity or greater will be logged to
syslog, stdout, and the rdbduprunner log file.

=item {B<--maxinc> I<integer>}

Acts as if the MaxInc config option were applied to every backup in
the configuration, but the backup specific MaxInc still takes
precendence for any given backup.

=item {B<--maxage> I<timespec>}

Acts as if the MaxAge config option were applied to every backup in
the configuration, but the backup specific MaxAge still takes
precendence for any given backup.

=item {B<--stats>|B<--nostats>|B<--no-stats>}

Corresponds to and supersedes the Stats configuration item, see below.

=item {B<--tempdir> I<path>}

Pass the --tempdir or --temp-dir options to underlying binaries in the
same manner as if the TempDir global config option had been specified.

=item {B<--full>} (duplicity only)

Passess the full option to duplicity backup.

=item {B<--force>} (duplicity only)

Passess the --force option to duplicity cleanup mode.

=item {B<--config> I<path>}

Change path to configuration file.

=item {B<--maxprocs> I<integer>}

Default: 1
Maximum number of simultaneous backups to perform.  rdbduprunner will
never do more than one backup from any given host at a time but if
there are multiple hosts multiple backups should occur at once.
See MaxProcs global config option.

=item {B<--maxwait> I<integer>}

Default: 86400
Maximum number of seconds to wait for a lock on any given per-host
lock file.
See MaxWait global config option.

=item {B<--allow-source-mismatch>} (duplicity only)

Passes the identical option to duplicity.  No affect on other backup
types.

=item {B<-n|--dry-run>} (rsync only)

Passes the --dry-run (-n) option to rsync.  rdbduprunner will not run
the prerun, postrun or zfs create and zfs snapshot commands (when
applicable.)

=item {B<--whole-file|--wholefile|--no-whole-file|--no-wholefile>} (rsync only)

Passes the --whole-file or --no-whole-file options to rsync.  This
supersedes the WholeFile configuration boolean specified in the config
file.

=item {B<--inplace|--no-inplace|--noinplace> - Boolean (true|t|yes|on|1), optional, rsync only

Corresponds to the Inplace configuration item, see below.

=item {B<-c|--checksum>} (rsync only)

Passes the --checksum (-c) option to rsync.

=item {B<--skipfs> I<filesystem>}

Adds the specified filesystem to the list of filesystems to be ignored
during inventory.

--skipfs reiserfs

=item {B<--allowfs> I<filesystem>}

Allows this filesystem to be included during inventory.  When
--allowfs is used, the skipfs list is disregarded.  Only filesystem
types specified this way will be included during inventory.

--allowfs reiserfs --allowfs ext4

=back

=head1 RETURN VALUE

=head1 ERRORS

=head1 DIAGNOSTICS

=head1 EXAMPLES

=head1 ENVIRONMENT

=head1 FILES

=head2 F<$HOME/.config/rdbduprunner/config> F</etc/rdbduprunner/rdbduprunner.conf>

rdbduprunner's config file is an Apache style config file parsed by
the Config::General perl module. It consists of global options,
options enclosed in a BackupDestination config section and options
enclosed in a BackupSet section.  The options names are case
insensitive. Options are generally honored in this order:

=head2 F<$HOME/.local/state/rdbduprunner.pid> F</var/run/rdbduprunner.pid>

Default PID/lock file.

=over 4

=item Command line options

=item BackupSet options

=item BackupDestination options

=item Global options

=back

=head3 Global Options

=over 4

=item I<DuplicityBinary> - path to duplicity

=item I<RdiffBackupBinary> - path to rdiff-backup

=item I<RsyncBinary> - path to rsync

=item I<ZfsBinary> - path to zfs

=item I<ExcludePath> - What directory to look for exclude
files. Defaults to F</etc/rdbduprunner>

=item I<UseAgent> - Boolean, optional
Passes the --use-agent option to duplicity.

=item I<TempDir> - Passed to some binaries as an alternate temp dir
path. Defaults to not pass any temp options therefore probably uses
/tmp.

=item I<DefaultBackupDestination> - Which backup destination to use if
none is specified.

=item I<MaxProcs> - Maxiumum number of backups to run simultaneously.

=item I<MaxWait> - Maxiumum number of seconds to wait for a host lock.

=item I<AllowFS> - String, Optional

Only inventory filesystems matching filesystem types specified via
this option.  Can be specified multiple times.  Can also be specified
at the BackupSet level.

=item I<WholeFile> - Boolean (default: false)

When true, pass --whole-file to rsync.
When false, pass --no-whole-file to rsync.

Can be specified at the BackupDestination and BackupSet levels as
well, which will override the global setting.

=item I<Stats> - Boolean (default: true)

Most backup binaries supported by rdbduprunner have the option to
produce some kind of statistics after a run.  Setting this to false
will turn those options off.

=back

=head3 BackupDestination Options

Items enclosed in a <BackupDestination X> configuration block. The
name of the block is referenced by the I<DefaultBackupDestination> global
option and the I<BackupDestination> config option inside BackupSet
blocks.

=over 4

=item I<PercentUsed> - Integer, optional

Keep disk usage on destination below this percent:
When using rdiff-backup and duplicty type backups rdbduprunner will
attempt to delete older increments in order to keep usage below this
percentage.  It will do so before running backups and will not run
backups if it cannot reduce usage below this level.

=item I<MinFree> - Integer, optional

Keep disk usage below this number: When using rdiff-backup and
duplicty type backups rdbduprunner will attempt to delete older
increments in order to keep this much space free as specified in 512
byte blocks (because I could reliably get df to report this value.) It
will do so before running backups and will not run backups if it
cannot free this much space.

=item I<Inplace> - Boolean (true|t|yes|on|1), optional, rsync only

Passes the --inplace and --partial options to rsync (instead of
--sparse) for this destination.  Assumed to be true every day of the
week except Sunday unless explicitly set.  If you always want
--inplace, set this to true, if you always want --sparse set this to
false.

=back

=head3 BackupSet Options

Items enclosed in a <BackupSet X> configuration block. The name of the
block is not referenced anywhere but should be unique or collisions
will occur.

=over 4

=item I<BackupDestination> - String, optional

Must match existing BackupDestination as defined above.  When not
specified the default will be used as specified in global options as
DefaultBackupDestination.  These backups will be written to the the
destination specified using the backup type and other options
specified for that backup destination.

=item I<Path> - String, mandatory (multiples ok)

Directory to be backed up. This may be specified multiple times per
backupset, and doing so will create multiple backups one for each path
specified.

=item I<Host> - String, optional

Host to be backed up; if not specified assumes localhost

=item I<Inventory> - Boolean (if present at all, assumed to be true!)

Look at the remote machine for a list of directories to back up:
Excludes a long list of non-filesystem type filesystems (fuse, etc.)
Creates a new backup from each one not excluded.

=item I<Tag> - String, optional

Overrides the generated tag used to construct the output directory path.

=item I<Exclude> - String, optional (multiples ok)

Pass these paths to the --exclude or similar options of the underlying
invocation of rdiff-backup, duplicity, or rsync.  Does not attempt to
normalize the paths for any given method of specifiying exclusions.

=item I<Skip> - String, optional (multiples ok)

If a path added via the inventory process matches exactly the string
or strings specified by a Skip option, do not back up that
filesystem/mountpoint. The following configuration fragment will cause
rdbduprunner to backup all filesystems except /tmp F<if> and only if /tmp
is a seperate filesystem.  It does not work as an exclude directive
above, it only matches filesystem mount points:

 Inventory true
 Skip /tmp

=item I<SkipRE> - String, optional (multiples ok)

If a path added via the inventory process matches the string specified
by a SkipRE option when treated as a regular expression, rdbduprunner
will not not back up that filesystem/mountpoint. The following
configuration fragment will cause rdbduprunner to back up all
filesystems except filesystems mounted under /mnt, but only if they
are a seperate unique filesystem added via inventory:

 Inventory true
 SkipRE /mnt/.+

If there is a directory, in this example, like /mnt/test it will still
be backed up unless it it's own mountpoint.  It does not work as an
exclude directive as documented above above, it only matches
filesystem mount points:

=item I<MaxInc> - Integer, optional

Used by the --tidy option to remove all but the most recent X
increments.

=item I<MaxAge> - String, optional (rdiff-backup/duplicity only)

Used by the --tidy option to remove all increments older than
specified age.  Ultimately this is passed to the remove-older-than
options of rdiff-backup or duplicity so see those manpages for
details.

=item I<ZfsCreate> - Boolean, optional

Create a zfs for the path, if the directory doesn't already exist.
Can be set at the BackupDestination level as well.

=item I<ZfsSnapshot> - Boolean, optional

Create a zfs snapshot of the destination path, if possible.
Can be set at the BackupDestination level as well.

=item I<Inplace> - Boolean (true|t|yes|on|1|false|f|no|off|0), optional

Passes the --inplace and --partial options to rsync for this
destination.  Assumed to be false unless Inplace is set to one of the
above parameters. Defaults to false.
Can be set at the BackupDestination level as well.

=item I<AllowFS> - String, optional

Only inventory filesystems matching filesystem types specified via
this option.  Can be specified multiple times.  Specified at the
BackupSet level will completely override the list of filesystem types
specified at the global level.

=back

=head1 CAVEATS

=head1 BUGS

=head1 RESTRICTIONS

=head1 NOTES

rdbduprunner *should* prefer to use configuration items in this order,
with later entries superseding the prior level. This assumes that the
option itself is applicable all the way down to the individual
invocations of the backup software.

=item Global Configuration

=item BackupDestination

=item BackupSet

=item Command Line Options

=head1 AUTHOR

Aran Cox <arancox@gmail.com>

=head1 HISTORY

=head1 SEE ALSO

=over 4

rdiff-backup(1), duplicity(1), rsync(1), trickle(1), ssh(1), unison(1).

=back
