#! /usr/bin/perl
# Copyright Aran Cox <arancox@gmail.com>

use strict;
use warnings;
use Getopt::Long;
use logentry;
use Fcntl qw(:DEFAULT :flock); # import LOCK_* constants
use Env qw( HOME );
use Config::General;
use Data::Dumper;

our $TEST=1;
our $VERBOSITY=5;
our $LOCALHOST;
our $COMPARE=0;
our $DUPLICITY_BINARY;
our $EXCLUDE_PATH='/etc/rdiff-backup';
our $PATH;
our $DEST;
our ($VERBOSE,$DEBUG);
our $CLEANUP=0;
our $FULL=0;
our $STATUS=0;
our $TIDY=0;
our $DUMP=0;
our $MAXAGE;
our $LOCK_FILE="$HOME/duplicity-runner.pid";

our $CONFIG_FILE="$HOME/.duplicity-runner.rc";
our %CONFIG;
our %BACKUPS;

GetOptions('test!'          => \$TEST,
           'verbosity=i'    => \$VERBOSITY,
           'localhost=s'    => \$LOCALHOST,
           'duplicity-binary=s' => \$DUPLICITY_BINARY,
           'compare'        => \$COMPARE,
           'path=s'         => \$PATH,
	   'dest=s'         => \$DEST,
           'verbose!'       => \$VERBOSE,
           'debug!'         => \$DEBUG,
	   'config=s'       => \$CONFIG_FILE, # config file
	   'cleanup'        => \$CLEANUP,
	   'full'           => \$FULL,
	   'status'         => \$STATUS,
	   'tidy'           => \$TIDY,
	   'dump'           => \$DUMP,
	   'maxage=s'       => \$MAXAGE,
	   'lockfile=s'     => \$LOCK_FILE,
          );

if(not defined $LOCALHOST) {
  if(defined $CONFIG{localhost}) {
    $LOCALHOST=$CONFIG{localhost};
  } else {
    $LOCALHOST=`hostname`;
    chomp $LOCALHOST;
    my @a=split(/\./,$LOCALHOST);
    @a > 1 and $LOCALHOST=$a[0];
  }
}

logentry::OpenLog('local5','duplcity-runner');
logentry('info','starting');

%CONFIG=new Config::General(-ConfigFile => $CONFIG_FILE,
			    -UseApacheInclude => 1,
			    -AutoTrue => 1,
			    -LowerCaseNames => 1)->getall() or die "unable to parse $CONFIG_FILE";

if(not defined $DUPLICITY_BINARY) {
    if(defined $CONFIG{duplicitybinary}) {
	$DUPLICITY_BINARY=$CONFIG{duplicitybinary};
    } else {
	$DUPLICITY_BINARY='duplicity'; # in our path we hope
    }
}
unless(-x $DUPLICITY_BINARY) {
    logndie("unable to execute $DUPLICITY_BINARY");
}

# grab more stuff from the config and put them into ENV for use by duplicity
if(defined $CONFIG{gpgpassphrase}) {
  $ENV{PASSPHRASE}=$CONFIG{gpgpassphrase};
}
if(defined $CONFIG{awsaccesskeyid}) {
  $ENV{AWS_ACCESS_KEY_ID}=$CONFIG{awsaccesskeyid};
}
if(defined $CONFIG{awssecretaccesskey}) {
  $ENV{AWS_SECRET_ACCESS_KEY}=$CONFIG{awssecretaccesskey};
}


foreach my $bs (values(%{$CONFIG{backupset}})) {
  my $host=$LOCALHOST;

    my $backupdest;
    if(defined $$bs{backupdestination}) {
	$backupdest=$$bs{backupdestination};
    } elsif(defined $CONFIG{defaultbackupdestination}) {
	$backupdest=$CONFIG{defaultbackupdestination};
    }
    unless(defined $backupdest and defined $CONFIG{backupdestination}{$backupdest}) {
      # can't easily verify the existance of all backup destinations without using duplicity itself
	logndie("no backup root defined: $backupdest".(defined $CONFIG{backupdestination}{$backupdest}{path} ? " ($CONFIG{backupdestination}{$backupdest}{path})" : ''));
    }
    if(defined $DEST and $backupdest !~ /$DEST/) {
	next;
    }
    my $backupdestpath=$CONFIG{backupdestination}{$backupdest}{path};

    foreach my $path (ref($$bs{path}) eq "ARRAY" ? @{$$bs{path}} : ($$bs{path})) {
      if(defined $PATH and $path !~ /$PATH/) {
	  next;
      }
      my $dest;
      my $tag='';
      if(defined $$bs{tag}) {
	  $dest=$$bs{tag};
	  $tag=$dest;
      } else {
	  $dest=$path;
	  $dest =~ s/\//\-/g;
	  $dest =~ s/ /_/g;
	  $dest eq '-' and $dest='-root';
	  $tag=$host.$dest;
      }
      # I should use a perl module here, I guess, not .
      $dest=$backupdestpath.'/'.$tag;
      my @tags=split(/\-/,$tag);

      logentry('debug',"Host: $host Path: $path Tag: $tag Dest: $dest Root: $backupdestpath");

      $BACKUPS{$tag}={%{$bs}}; # very important to make a copy here
      $BACKUPS{$tag}{dest}=$dest;
      $BACKUPS{$tag}{path}=$path;
      $BACKUPS{$tag}{tag}=$tag;
      $BACKUPS{$tag}{host}=$host;
      $BACKUPS{$tag}{backupdestination}=$backupdest;
      $BACKUPS{$tag}{gtag}='generic-'.$tags[1];

      if( -f $EXCLUDE_PATH.'/exclude-generic' ) {
	push(@{$BACKUPS{$tag}{excludes}},$EXCLUDE_PATH.'/exclude-generic');
      } else {
	#logentry('warn',"unable to find the exclude file: ".$EXCLUDE_PATH.'/exclude-'.$BACKUPS{$tag}{gtag}." for backup $tag");
      }

      if( -f $EXCLUDE_PATH.'/exclude-'.$BACKUPS{$tag}{gtag} ) {
	push(@{$BACKUPS{$tag}{excludes}},$EXCLUDE_PATH.'/exclude-'.$BACKUPS{$tag}{gtag});
      } else {
	#logentry('warn',"unable to find the exclude file: ".$EXCLUDE_PATH.'/exclude-'.$BACKUPS{$tag}{gtag}." for backup $tag");
      }

      if( -f $EXCLUDE_PATH.'/exclude-'.$tag ) {
	push(@{$BACKUPS{$tag}{excludes}},$EXCLUDE_PATH.'/exclude-'.$tag);
      } else {
	logentry('warn',"unable to find the exclude file: ".$EXCLUDE_PATH.'/exclude-'.$tag." for backup $tag");
      }
      if(defined $MAXAGE) {
 	  $BACKUPS{$tag}{maxage}=$MAXAGE;
      } elsif(not defined $BACKUPS{$tag}{maxage} and
 	      defined $CONFIG{maxage}) {
 	  $BACKUPS{$tag}{maxage}=$CONFIG{maxage};
      } else {
	  $BACKUPS{$tag}{maxage}='60D';
      }
    }
}

if($DUMP) {
    print Dumper \%BACKUPS;
    logndie "you asked me to dump and die!";
}

if($STATUS) {
    foreach my $tag (sort backup_sort (keys(%BACKUPS))) {
	my @com=($DUPLICITY_BINARY,'collection-status',
		 '--verbosity',$VERBOSITY,
		 $BACKUPS{$tag}{dest});
	logentry('info',join(" ",@com));
	unless($TEST) {
	    lock_pid_file();
	    system(@com);
	    unless($? == 0) {
		logentry('err',"unable to execute duplicity!");
	    }
	    unlock_pid_file();
	}
    }
} elsif($CLEANUP) {
    foreach my $tag (sort backup_sort (keys(%BACKUPS))) {
	my @com=($DUPLICITY_BINARY,'cleanup',
		 '--verbosity',$VERBOSITY,
		 $BACKUPS{$tag}{dest});
	logentry('info',join(" ",@com));
	unless($TEST) {
	    lock_pid_file();
	    system(@com);
	    unless($? == 0) {
		logentry('err',"unable to execute duplicity!");
	    }
	    unlock_pid_file();
	}
    }
} elsif($TIDY) {
    foreach my $tag (sort backup_sort (keys(%BACKUPS))) {
	tidy($tag);
    }
} else { # actually do a backup, please
  BACKUP:
    foreach my $tag (sort backup_sort (keys(%BACKUPS))) {
	my $bh=$BACKUPS{$tag}; # for convenience of typing

	if($$bh{disabled}) {
	    # skip disabled backups
	    next;
	}

	if(not -d $BACKUPS{$tag}{path}) {
	  logentry('warn',"backup path $BACKUPS{$tag}{path} does not exist: skipping this backup");
	  next BACKUP;
	}

	if(defined $CONFIG{$$bh{backupdestination}}{busted} and $CONFIG{$$bh{backupdestination}}{busted}) {
	    next BACKUP;
	}

	my @com=($DUPLICITY_BINARY);
	$FULL and push(@com,'full');
	push(@com,
	     '--verbosity',$VERBOSITY,
	     '--exclude-other-filesystems',
	    );
	if(defined $CONFIG{signkey}) {
	  push(@com,'--sign-key',$CONFIG{signkey});
	}
	if(defined $CONFIG{encryptkey}) {
	  push(@com,'--encrypt-key',$CONFIG{encryptkey});
	}

	not $VERBOSE and push(@com,'--no-print-statistics');

	# if($COMPARE) {
	#     push(@com,'--compare');
	# }

	foreach my $f (@{$BACKUPS{$tag}{excludes}}) {
	    push(@com,'--exclude-globbing-filelist',
		 $f);
	}
	# this is fugly:
	push(@com,$BACKUPS{$tag}{path},$BACKUPS{$tag}{dest});

	logentry('info',join(" ",@com));

	unless($TEST) {
	    lock_pid_file();

	    if (defined $$bh{prerun}) {
		print $$bh{prerun}."\n";
		system($$bh{prerun});
		unless($? == 0) {
		    logentry('err',"unable to execute prerun command: skipping backup!");
		    next;
		}
	    }
	    system(@com);
	    my $mainret=$?;
	    unless($mainret == 0) {
		logentry('err',"unable to execute duplicity!");
	    }
	    if(defined $$bh{postrun}) {
		print $$bh{postrun}."\n";
		system($$bh{postrun});
		unless($? == 0) {
		    logentry('err',"unable to execute postrun command");
		}
		unlock_pid_file();
	    }
	}
    }
}



sub backup_sort {
    my $aprio=(defined $BACKUPS{$a}{priority} ? $BACKUPS{$a}{priority} : 0);
    my $bprio=(defined $BACKUPS{$b}{priority} ? $BACKUPS{$b}{priority} : 0);
    
    return $aprio <=> $bprio;
}

#sub find_backup_by_tag {
#    foreach my $hr (@BACKUPS) {
#	if

sub lock_pid_file {
    if(defined $LOCK_FILE) {
	# leave it alone
    } elsif(defined $CONFIG{LockFile}) {
	$LOCK_FILE=$CONFIG{LockFile};
    } else {
	$LOCK_FILE="$HOME/duplicity-runner.pid";
    }
    unless(open(LOCK,'+<'.$LOCK_FILE) or open(LOCK,'>'.$LOCK_FILE)) {
	logentry('err',"unable to open pid file: $LOCK_FILE for writing");
	next;
    }
    unless(flock(LOCK,LOCK_EX|LOCK_NB)) {
	logentry('err',"unable to flock the pid file: $LOCK_FILE");
	next;
    }
    truncate(LOCK,0); # this shouldn't fail if we have the file opened and locked!
    print LOCK $$."\n"; # who really cares if this fails?
}

sub unlock_pid_file {
    unlink $LOCK_FILE;
    flock(LOCK,LOCK_UN);
    close LOCK;
}

sub tidy {
    my $tag=$_[0];
    my @com=($DUPLICITY_BINARY,
	     'remove-older-than',
	     $BACKUPS{$tag}{maxage},
	     '--force',
	     '--verbosity',$VERBOSITY,
	     $BACKUPS{$tag}{dest}
	);
    logentry('info',join(" ",@com));
    unless($TEST) {
	system(@com);
	unless($? == 0) {
	    logentry('err',"unable to execute duplicity!");
	}
    }
}

